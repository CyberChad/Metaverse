sp {move-block*elaborate*problem-space*ss-top-state
   (state <s> ^name move-block
              ^superstate <ss>)
   (<ss> ^operator.name evaluate-operator
         ^superstate.top-state <ssts>)
-->
   (<s> ^ss-top-state <ssts>)
}

sp {move-block*elaborate*problem-space*top-state
   (state <s> ^name move-block
              ^ss-top-state <ts>
              ^superstate <ss>)
   (<ss> ^operator.name evaluate-operator)
   (<ts> ^object <tblock1> <tblock2> <tblock3> <table>
         ^gripper <gripper>)
   (<tblock1> ^name A)
   (<tblock2> ^name B)
   (<tblock3> ^name C)
   (<table> ^name table)
-->
   (<s> ^top-state <tsn>)
   (<tsn> ^flag move-block-lookahead
          ^gripper <ngripper>
          ^object <tblock1> <tblock2> <tblock3> <table>)
}

sp {move-block*elaborate*problem-space*top-state*both
   (state <s> ^name move-block
              ^ss-top-state <ts>
              ^top-state <nts>
              ^superstate <ss>)
   (<ss> ^operator.name evaluate-operator)
   (<ts> ^ontop <ontop1>)
   (<ontop1> ^top-block <tblock1>
             ^bottom-block <bblock1>)
-->
   (<nts> ^ontop <nontop3>)
   (<nontop3> ^top-block <tblock1>
              ^bottom-block <bblock1>)
}

sp {move-block*elaborate*problem-space*top-state*one
   (state <s> ^name move-block
              ^ss-top-state <ts>
              ^top-state <nts>
              ^superstate <ss>)
   (<ss> ^operator.name evaluate-operator)
   (<ts> ^ontop <ontop1>)
   (<ontop1> ^top-block <tblock1>
            -^bottom-block <bblock1>)
-->
   (<nts> ^ontop <nontop3>)
   (<nontop3> ^top-block <tblock1>)
}

sp {move-block*elaborate*problem-space*top-state*gripper*holding
   (state <s> ^name move-block
              ^ss-top-state <ts>
              ^top-state <nts>
              ^superstate <ss>)
   (<nts> ^gripper { <> <gripper> <ngripper> })
   (<ss> ^operator.name evaluate-operator)
   (<ts> ^gripper <gripper>)
   (<gripper> ^holding <holding>)
-->
   (<ngripper> ^holding <holding>)}

sp {move-block*elaborate*problem-space*top-state*gripper
   (state <s> ^superstate <ss>
              ^ss-top-state <ts>
              ^name move-block
              ^top-state.gripper { <> <gripper> <ngripper> })
   (<ss> ^operator.name evaluate-operator)
   (<ts> ^gripper <gripper>)
   (<gripper> ^above <above>
              ^open <open>
              ^position <position>)
-->
   (<ngripper> ^above <above>
               ^open <open>
               ^position <position>)}

######################
sp {evaluate-operator*elaborate*state*tried-tied-operator*pickup*putdown*rules
   :default
   (state <s> ^operator <o>
              ^look-ahead-operator <o>)
   (<o> ^name << pick-up put-down >>)
-->
   (<s> ^tried-tied-operator <o>)}
