
sp {mac*propose*operator*move-boat1m
   "Moves a single missionary."
   (state <s> ^name mac
              ^boat <bank>
              ^<bank>.missionaries > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name move-boat
        ^missionaries 1
        ^cannibals 0)}


sp {mac*propose*operator*move-boat1c
   "Moves a single cannibal."
   (state <s> ^name mac
              ^boat <bank>
              ^<bank>.cannibals > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name move-boat
        ^cannibals 1
        ^missionaries 0)}


sp {mac*propose*operator*move-boat2m
   "Moves two missionaries."
   (state <s> ^name mac
              ^boat <bank>
              ^<bank>.missionaries > 1)
-->
   (<s> ^operator <o> +)
   (<o> ^name move-boat
        ^missionaries 2
        ^cannibals 0)}


sp {mac*propose*operator*move-boat2c
   "Moves two cannibals."
   (state <s> ^name mac
              ^boat <bank>
              ^<bank>.cannibals > 1)
-->
   (<s> ^operator <o> +)
   (<o> ^name move-boat
        ^cannibals 2
        ^missionaries 0)}

sp {mac*propose*operator*move-boat11
   (state <s> ^name mac
              ^boat <bank>
              ^<bank> <b>)
   (<b> ^missionaries > 0
        ^cannibals > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name move-boat
        ^missionaries 1
        ^cannibals 1)}

###
### move-boat OPERATOR IMPLEMENTATION
###
#
sp {apply*move-boat*not-1missionary-1-cannibal
   (state <s> ^operator <o>                        
              ^boat <bank>
              ^o-bank <o-bank>
              ^<bank> <b> 
              ^<o-bank> <ob>)
   (<b> ^<type> <bank-num>)
   (<ob> ^<type> <o-bank-num>)
   (<o> ^name move-boat
        ^{ << missionaries cannibals >> <type> } { <> 0 <num> }
        ^{ << missionaries cannibals >> <o-type> } 0)
-->
   (<b> ^<type> <bank-num> -
               (- <bank-num> <num>))
   (<ob> ^<type> <o-bank-num> -
               (+ <o-bank-num> <num>))
   (<s> ^boat <bank> -
              <o-bank>
        ^o-bank <o-bank> -
                <bank>)
}

sp {apply*move-boat*1-missionary-1-cannibal*right
   (state <s> ^operator <o>                        
              ^boat right-bank
              ^o-bank left-bank
              ^right-bank <b> 
              ^left-bank <ob>)
   (<b> ^missionaries <m-num>
        ^cannibals <c-num>)
   (<ob> ^missionaries <o-m-num>
         ^cannibals <o-c-num>)
   (<o> ^name move-boat
        ^missionaries 1
        ^cannibals 1)
-->
   (<b> ^missionaries <m-num> -
                      (- <m-num> 1)
        ^cannibals <c-num> -
                      (- <c-num> 1))
   (<ob> ^missionaries <o-m-num> -
                      (+ <o-m-num> 1)
         ^cannibals <o-c-num> -
                      (+ <o-c-num> 1))
   (<s> ^boat right-bank -
              left-bank
        ^o-bank left-bank -
                right-bank)
}

sp {apply*move-boat*1-missionary-1-cannibal*left
   (state <s> ^operator <o>                        
              ^boat  left-bank
              ^o-bank right-bank
              ^left-bank <b> 
              ^right-bank <ob>)
   (<b> ^missionaries <m-num>
        ^cannibals <c-num>)
   (<ob> ^missionaries <o-m-num>
         ^cannibals <o-c-num>)
   (<o> ^name move-boat
        ^missionaries 1
        ^cannibals 1)
-->
   (<b> ^missionaries <m-num> -
                      (- <m-num> 1)
        ^cannibals <c-num> -
                      (- <c-num> 1))
   (<ob> ^missionaries <o-m-num> -
                      (+ <o-m-num> 1)
         ^cannibals <o-c-num> -
                      (+ <o-c-num> 1))
   (<s> ^boat left-bank -
               right-bank 
        ^o-bank right-bank -
                left-bank)
}