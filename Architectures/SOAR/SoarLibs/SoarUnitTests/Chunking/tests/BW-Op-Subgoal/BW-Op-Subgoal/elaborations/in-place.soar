sp {P3*elaborate*object-on-table
   (state <s> ^name blocks-world ^object <table>)
   (<table> ^name table)
-->
   (<s> ^inplace-object <table>)
}

sp {P4*elaborate*object-on-inplace
   (state <s> ^name blocks-world ^inplace-object <blockB> ^desired <d*1>
              ^ontop <ontop-s>)
   (<ontop-s> ^bottom-block <blockB> ^top-block <blockA>)
   (<d*1> ^ontop <ontop-d>)
   (<ontop-d> ^bottom-block <blockB> ^top-block <blockA>)
-->
   (<s> ^inplace-object <blockA>)
}

# This production says that an object is out of place if it is on another
# out of place objects, even if it's supposed to be on that object in the
# desired configuration

sp {elaborate*object-out-of-place1
   (state <s> ^name blocks-world ^outplace-object <blockB> ^desired <d*1>
              ^ontop <ontop-s>)
   (<ontop-s> ^bottom-block <blockB> ^top-block <blockA>)
   (<d*1> ^ontop <ontop-d>)
   (<ontop-d> ^bottom-block <blockB> ^top-block <blockA>)
-->
   (<s> ^outplace-object <blockA>)
}

# This production says that an object is out of place if it is not on its
# desired block in the final configuration

sp {elaborate*object-out-of-place2
   (state <s> ^name blocks-world ^inplace-object <blockB> ^desired <d*1>
#   (state <s> ^name blocks-world ^desired <d*1>
              ^ontop <ontop-s>)
   (<d*1> ^ontop <ontop-d>)
   (<ontop-d> ^bottom-block <blockB> ^top-block <blockA>)
  -{ (<ontop-s> ^bottom-block <blockB> ^top-block <blockA>)}
-->
   (<s> ^outplace-object <blockA>)
}
