epmem --set trigger dc
epmem --set learning on
epmem --set balance 0

wma --set activation on

watch --epmem

### initialize
sp {propose*init
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <op> + >)
   (<op> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^cycle-count 2
        ^query <query>
        ^name test-wme-length)
   (<query> ^cycle-count 2
            ^cycle-count 3)
}

### Count Operators

sp {propose*count
   (state <s> ^name test-wme-length
              ^cycle-count <c>
              ^query <query>)
   (<query> ^cycle-count 2)
-->
   (<s> ^operator <count> + <)
   (<count> ^name count-cycles
            ^cycle-count <c>)
}

sp {apply*count*remove-cycle-count
   (state <s> ^name test-wme-length
              ^operator.name count-cycles
              ^cycle-count <c>)
-->
   (<s> ^cycle-count <c> -)
}

sp {apply*count*increment-count
   (state <s> ^name test-wme-length
              ^operator <count>)
   (<count> ^name count-cycles
            ^cycle-count <c>)
-->
   (<s> ^cycle-count (+ <c> 1))
}

### query when sufficient counting
sp {propose*query
   (state <s> ^name test-wme-length
             -^epmem.command.query <q>
              ^cycle-count 4)
-->
   (<s> ^operator <query-operator> + >)
   (<query-operator> ^name query)
}

sp {apply*query
   (state <s> ^operator.name query
              ^epmem.command <cmd>
              ^query <query>)
-->
   (<cmd> ^query <query>)
}

### halt when query returns
sp {propose*check-epmem*success
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>
            ^result.retrieved <result>)
   (<result> ^cycle-count 2)
-->
   (<s> ^operator <check-epmem> + >)
   (<check-epmem> ^name check-epmem
                  ^status success)
}

sp {propose*check-epmem*failure*query-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.failure|)
}

sp {propose*check-epmem*failure*query-result-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.success - unexpected result|)
}

sp {apply*check-epmem*success
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status success)
-->
   (write |Success!| (crlf))
   (halt)
}

sp {apply*check-epmem*failure
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status failure
                  ^condition <condition>)
-->
   (write |Failed because '| <condition> |'!| (crlf))
   (halt)
}
