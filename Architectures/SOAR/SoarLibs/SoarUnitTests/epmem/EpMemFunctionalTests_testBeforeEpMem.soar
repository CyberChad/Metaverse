epmem --set trigger dc
epmem --set learning on
watch --epmem

### initialize
sp {propose*init
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator.name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^counter 1
        ^max 10
        ^first true
        ^name count)
}

### indicate even vs. odd
sp {elab*mod2
   (state <s> ^counter <c>)
-->
   (<s> ^mod2 (mod <c> 2))
}

sp {elab*even
   (state <s> ^mod2 0)
-->
   (<s> ^even t)
}

sp {elab*odd
   (state <s> ^mod2 1)
-->
   (<s> ^odd t)
}

### count up to max (clear first if still around)
sp {propose*next
   (state <s> ^name count
              ^counter <c>
              ^max {<> <c>})
-->
   (<s> ^operator.name next)
}

sp {apply*next
   (state <s> ^operator.name next
              ^counter <c>)
-->
   (<s> ^counter <c> -) 
   (<s> ^counter (+ <c> 1))
}

sp {apply*next*first
   (state <s> ^operator.name next
              ^first <f>)
-->
   (<s> ^first <f> -)
}

### query when done counting
sp {propose*query
   (state <s> ^name count
              ^counter <m>
              ^max <m>
             -^epmem.command.query)
-->
   (<s> ^operator.name query)
}

sp {apply*query
   (state <s> ^operator.name query
              ^epmem.command <cmd>
              ^max <m>)
-->
   (<cmd> ^query <q>
          ^prohibit 1 2 4 8
          ^before 10)
   (<q> ^even t)
}

### halt when query returns
sp {propose*check-epmem*success
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>
            ^result.retrieved <result>)
   (<result> ^counter 6
             ^even t)
-->
   (<s> ^operator <check-epmem> + >)
   (<check-epmem> ^name check-epmem
                  ^status success)
}

sp {propose*check-epmem*failure*query-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.failure|)
}

sp {propose*check-epmem*failure*query-result-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.success - unexpected result|)
}

sp {apply*check-epmem*success
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status success)
-->
   (write |Success!| (crlf))
   (succeeded)
}

sp {apply*check-epmem*failure
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status failure
                  ^condition <condition>)
-->
   (write |Failed because '| <condition> |'!| (crlf))
   (failed)
}
