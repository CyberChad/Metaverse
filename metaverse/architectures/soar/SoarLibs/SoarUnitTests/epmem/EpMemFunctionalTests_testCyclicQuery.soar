epmem --set trigger dc
epmem --set learning on
watch --epmem

### initialize
sp {propose*init
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <op> + >)
   (<op> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^in-memory false
        ^A <A>
        ^name cylic-query)
   (<A> ^B <B>)
   (<B> ^C <C>)
   (<C> ^A <A>)
}

### remove float once in memory

sp {propose*remove-item1
   (state <s> ^name cylic-query
              ^A <A>)
-->
   (<s> ^operator <op> +)
   (<op> ^name remove-item1)
}

sp {apply*remove-item1
   (state <s> ^operator.name remove-item1
              ^A <A>
              ^in-memory <in>)
-->
   (<s> ^A <A> -
        ^in-memory <in> -
        ^in-memory true)
}

### query when done counting
sp {propose*query
   (state <s> ^name cylic-query
              ^in-memory true
             -^epmem.command.query)
-->
   (<s> ^operator.name query)
}

sp {apply*query
   (state <s> ^operator.name query
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^A <A>)
   (<A> ^B <B>)
   (<B> ^C <C>)
   (<C> ^A <A>)
}

### halt when query returns
sp {propose*check-epmem*success
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure)
-->
   (<s> ^operator <check-epmem> + >)
   (<check-epmem> ^name check-epmem
                  ^status success)
}

sp {propose*check-epmem*failure
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.success - unexpected result|)
}

sp {apply*check-epmem*success
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status success)
-->
   (write |Success!| (crlf))
   (succeeded)
}

sp {apply*check-epmem*failure
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status failure
                  ^condition <condition>)
-->
   (write |Failed because '| <condition> |'!| (crlf))
   (failed)
}
