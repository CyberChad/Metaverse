epmem --set trigger dc
epmem --set learning on
watch --epmem

### initialize
sp {propose*init
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <op> + >)
   (<op> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^in-memory false
        ^pi 3.14159265358979323846 #This truncates at 3.141592653589793
        ^name double-precision)
}

### remove pi once in memory

sp {propose*remove-pi
   (state <s> ^name double-precision
              ^pi <pi>)
-->
   (<s> ^operator <op> +)
   (<op> ^name remove-pi)
}

sp {apply*remove-pi
   (state <s> ^operator.name remove-pi
              ^pi <pi>
              ^in-memory <in>)
-->
   (<s> ^pi <pi> -
        ^in-memory <in> -
        ^in-memory true)
}

### query when done counting
sp {propose*query
   (state <s> ^name double-precision
              ^in-memory true
             -^epmem.command.query)
-->
   (<s> ^operator.name query)
}

sp {apply*query
   (state <s> ^operator.name query
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^pi 3.14159265358979323846)
}

### halt when query returns
sp {propose*check-epmem*success
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>
            ^result.retrieved <result>)
   (<result> ^pi { >= 3.14159265358979 <= 3.14159265358980 } )
-->
   (<s> ^operator <check-epmem> + >)
   (<check-epmem> ^name check-epmem
                  ^status success)
}

sp {propose*check-epmem*failure*query-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.failure|)
}

sp {propose*check-epmem*failure*query-result-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.success - unexpected result|)
}

sp {apply*check-epmem*success
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status success)
-->
   (write |Success!| (crlf))
   (succeeded)
}

sp {apply*check-epmem*failure
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status failure
                  ^condition <condition>)
-->
   (write |Failed because '| <condition> |'!| (crlf))
   (failed)
}
