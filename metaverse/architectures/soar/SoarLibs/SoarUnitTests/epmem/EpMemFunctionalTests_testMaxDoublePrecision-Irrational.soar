epmem --set trigger dc
epmem --set learning on
watch --epmem

### initialize
sp {propose*init
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <op> + >)
   (<op> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^in-memory false
        ^name double-precision)
}

sp {elaboration*divide-by-three
   (state <s> ^name double-precision)
-->
   (<s> ^integer (/ 2 3))
}

### remove integer once in memory

sp {propose*remove-integer
   (state <s> ^name double-precision
              ^integer <int>)
-->
   (<s> ^operator <op> +)
   (<op> ^name remove-integer)
}

sp {apply*remove-integer
   (state <s> ^operator.name remove-integer
              ^integer <int>
              ^in-memory <in>)
-->
   (<s> ^integer <int> -
        ^in-memory <in> -
        ^in-memory true)
}

### query when done counting
sp {propose*query
   (state <s> ^name double-precision
              ^in-memory true
             -^epmem.command.query)
-->
   (<s> ^operator.name query)
}

sp {apply*query
   (state <s> ^operator.name query
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^integer 0.66666666666666666666666666666666)
}

### halt when query returns
sp {propose*check-epmem*success
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>
            ^result.retrieved <result>)
   (<result> ^integer { >= 0.666666666666666 <= 0.666666666666667 })
-->
   (<s> ^operator <check-epmem> + >)
   (<check-epmem> ^name check-epmem
                  ^status success)
}

sp {propose*check-epmem*failure*query-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.failure|)
}

sp {propose*check-epmem*failure*query-result-fail
   (state <s> ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.success <q>)
-->
   (<s> ^operator <failure-op> +)
   (<failure-op> ^name check-epmem
                 ^status failure
                 ^condition |result.success - unexpected result|)
}

sp {apply*check-epmem*success
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status success)
-->
   (write |Success!| (crlf))
   (succeeded)
}

sp {apply*check-epmem*failure
   (state <s> ^operator <check-epmem>)
   (<check-epmem> ^name check-epmem
                  ^status failure
                  ^condition <condition>)
-->
   (write |Failed because '| <condition> |'!| (crlf))
   (failed)
}
